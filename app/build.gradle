apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 28
    defaultConfig {
        //noinspection GroovyAssignabilityCheck
        applicationId "me.arkadybazhanov.spacedust"
        minSdkVersion 22
        targetSdkVersion 28
        //noinspection GroovyAssignabilityCheck
        versionCode 1
        //noinspection GroovyAssignabilityCheck
        versionName "1.0"
        //noinspection GroovyAssignabilityCheck
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    
    packagingOptions {
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/kotlinx-coroutines-io.kotlin_module'
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        local {
            applicationIdSuffix ".local"
            versionNameSuffix "-local"
            getProps('./localconfig.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
        prod {
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            getProps('./prodconfig.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
    }
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.0"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-compat:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(path: ':core')
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation "io.ktor:ktor-client-android:$ktor_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses", "-Xuse-experimental=kotlin.Experimental"]
        freeCompilerArgs += "-Xuse-experimental=kotlinx.serialization.ImplicitReflectionSerializer"
    }
}